// Asegurarse de tener el usuario actual cargado
Set(currentUser, User());

// NO EXISTE lote pendiente -> Puedes generar un lote
Set(varLoteIDGenerado, Left(currentUser.Email, Find("@", currentUser.Email)) & Text(Now(), "yyymmdd-hhmm"));

// Crear una copia de solicitudes solo con estados pendientes
ClearCollect(
    colSolicitud,
    AddColumns(
        ShowColumns(
            Filter(
                sng_solicitudes,
                sng_solicitudEstado = AppEstados.AprobadoAprobador1
            ),
            'sng_solicitudAprobador1Email',
            'sng_solicitudAprobador1Nombre',
            'sng_solicitudImporte',
            'sng_solicitudBeneficiarioEmail',
            'cr8bd_manager',
            Solicitudes // Este es el ID de Dataverse
        ),
        SolicitudesID, Solicitudes // Le damos un alias legible
    )
);

// Marcar las solicitudes con el lote generado
ForAll(
    colSolicitud, 
    Patch(
        sng_solicitudes, 
        LookUp(sng_solicitudes, Solicitudes = ThisRecord.SolicitudesID),
        {
            sng_loteID: varLoteIDGenerado
        }
    )
);

// Generar un lote desde cero
ClearCollect(
    colResumenManager,
    AddColumns(
        GroupBy(
            colSolicitud,
            'cr8bd_manager',
            "Registros"
        ),
        NombreManager, First(Registros).'sng_solicitudAprobador1Nombre',
        Total, Sum(Registros, 'sng_solicitudImporte'),
        Usuarios_en_peticion, Concat(Registros, 'sng_solicitudBeneficiarioEmail', ";"),
        Usuarios_IDs, Concat(Registros, SolicitudesID, ";"),
        FechaAgrupacion, Now(),
        ID, varLoteIDGenerado
    )
);

// Guardar datos en Dataverse
ForAll(
    colResumenManager, 
    Patch(
        sng_Lotes,
        Defaults(sng_Lotes),
        {
            lote_LoteID: ID, 
            lote_Estado: "Pendiente", 
            lote_CantidadSolicitudes: CountRows(Registros), 
            lote_CreadoFecha: FechaAgrupacion,
            lote_CreadoPorEmail: currentUser.Email, 
            lote_ImporteTotal: Total
        }
    )
);

// Cerrar pop-up
Set(varPopUpAlertaLote, false);
